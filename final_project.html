<!doctype html>
<html>

<head>
<title>CS112 Final Project: Cook-Torrance BRDF and Disney Principled Shader</title>
<meta charset="utf-8">
<link href="css/pa2.css" rel="stylesheet">
<link href="css/spectrum.css" rel="stylesheet">

<!-- Useful 3rd party libraries -->
<script type="text/javascript" src="js/jquery-1.12.2.min.js"></script>
<script type="text/javascript" src="js/spectrum.js"></script>
<script type="text/javascript" src="js/glMatrix-0.9.5.js"></script>
<script type="text/javascript" src="js/webgl-obj-loader.js"></script>

<!-- Shader initialization utils -->
<script type="text/javascript" src="js/shader-utils.js"></script>

<!-- WebGL debugging utils -->
<script type="text/javascript" src="js/webgl-debug.js"></script>
<script type="text/javascript" src="js/debug-utils.js"></script>

<!-- Model data -->
<script type="text/javascript" src="meshes/teapot_obj.js"></script>
<script type="text/javascript" src="meshes/bunny_obj.js"></script>

<!-- WebGL functions -->
<script type="text/javascript" src="final_project_webgl.js"></script>

<!-- Other javascript functions -->
<script type="text/javascript" src="final_project.js"></script>


<!-- Simple vertex shader for drawing the light source as a point -->
<script id="shader-vs-light" type="x-shader/x-vertex">
    uniform mat4 uPMatrix;
    attribute vec3 aVertexPosition;

    void main(void) {
        gl_PointSize = 10.0;
        gl_Position = uPMatrix * vec4(aVertexPosition, 1.0);
    }
</script>

<!-- Common vertex shader for all reflectance models -->
<script id="shader-vs" type="x-shader/x-vertex">
    uniform mat4 uMVMatrix;             // Model-view matrix
    uniform mat4 uPMatrix;              // Projection matrix
    uniform mat4 uNMatrix;              // Normal matrix

    attribute vec3 aVertexPosition;     // Vertex position in object space
    attribute vec3 aVertexNormal;       // Vertex normal in object space

    varying vec3 vPosition;             // Vertex position (camera space)
    varying vec3 vNormal;               // Vertex normal (camera space)

    void main(void) {
        vec4 camSpacePosition = uMVMatrix * vec4(aVertexPosition, 1.0);
        vPosition = vec3(camSpacePosition);

        gl_Position = uPMatrix * camSpacePosition;

        vec4 camSpaceNormal = uNMatrix * vec4(aVertexNormal, 0.0);
        vNormal = vec3(camSpaceNormal);
    }
</script>

<!-- Fragment shader: light draing -->
<script id="shader-fs-light" type="x-shader/x-fragment">
    precision mediump float;

    void main(void) {
        gl_FragColor = vec4(1.0, 1.0, 0.0, 1.0);
    }
</script>

<!-- Fragment shader: white -->
<script id="shader-fs0" type="x-shader/x-fragment">
    precision mediump float;

    varying vec3 vPosition;             // Vertex position (camera space)
    varying vec3 vNormal;               // Vertex normal (camera space)

    void main(void) {
        // Dummy variable to ensure the use of all vertex attributes.
        vec4 zero = vec4(vPosition + vNormal - vPosition - vNormal, 0.0);

        gl_FragColor = zero + vec4(1.0, 1.0, 1.0, 1.0);
    }
</script>

<!-- Fragment shader: diffuse model -->
<script id="shader-fs2" type="x-shader/x-fragment">
    precision mediump float;

    uniform vec3 uLightPos;             // Light position in camera space
    uniform float uLightPower;          // Light power
    uniform vec3 uDiffuseColor;         // Diffuse color
    uniform float uAmbient;             // Ambient

    varying vec3 vPosition;             // Fragment position (camera space)
    varying vec3 vNormal;               // Fragment normal (camera space)

    void main(void) {
        // Dummy variable to ensure the use of all vertex attributes.
        vec4 zero = vec4(vPosition + vNormal - vPosition - vNormal, 0.0);
        
        vec3 iVector = uLightPos - vPosition;       
        float intensity = uLightPower / ((length(iVector) * length(iVector)) / 5.0 + 5.0);

        float maxValue = max(dot(normalize(vNormal), normalize(iVector)), 0.0);
        float diffuseScalar = intensity * maxValue + uAmbient; 

        // Task 2
        gl_FragColor = zero + vec4(uDiffuseColor * diffuseScalar, 1.0);

    }
</script>

<!-- Fragment shader: Phong model -->
<script id="shader-fs3-1" type="x-shader/x-fragment">
    precision mediump float;

    uniform vec3 uLightPos;             // Light position in camera space
    uniform float uLightPower;          // Light power
    uniform vec3 uDiffuseColor;         // Diffuse color
    uniform float uExponent;            // Phong exponent
    uniform float uAmbient;             // Ambient

    varying vec3 vPosition;             // Fragment position (camera space)
    varying vec3 vNormal;               // Fragment normal (camera space)

    void main(void) {

        // Calculate all values needed to compute fragColor 
        vec3 normalizedIVector = normalize(uLightPos - vPosition);
        vec3 normalizedNVector = normalize(vNormal);
        float dotProductNI = dot(normalizedNVector, normalizedIVector);
        vec3 normalizedOVector = -normalize(vPosition);
        vec3 normalizedRVector = 2.0 * normalizedNVector * dotProductNI - normalizedIVector;
        float intensity = uLightPower / (pow(length(uLightPos - vPosition), 2.0) / 5.0 + 5.0);
        float maxValue = max(dot(normalizedOVector, normalizedRVector), 0.0);
        
        // Get the fragColor based on the value of the dotProductNI
        if(dotProductNI > 0.0)
        {
            gl_FragColor = vec4(uDiffuseColor * (intensity * dotProductNI + uAmbient) + (intensity * pow(maxValue, uExponent)), 1.0);
        }
        else
        {
            gl_FragColor = vec4(uDiffuseColor * uAmbient, 1.0);
        }
    }
</script>

<!-- Fragment shader: Blinn-Phong model -->
<script id="shader-fs3-2" type="x-shader/x-fragment">
    precision mediump float;

    uniform vec3 uLightPos;             // Light position in camera space
    uniform float uLightPower;          // Light power
    uniform vec3 uDiffuseColor;         // Diffuse color
    uniform float uExponent;            // Blinn-Phong exponent
    uniform float uAmbient;             // Ambient

    varying vec3 vPosition;             // Fragment position (camera space)
    varying vec3 vNormal;               // Fragment normal (camera space)

    void main(void) {
        // Calculate all values needed to compute fragColor 
        vec3 normalizedIVector = normalize(uLightPos - vPosition);
        vec3 normalizedNVector = normalize(vNormal);
        float dotProductNI = dot(normalizedNVector, normalizedIVector);
        vec3 normalizedOVector = -normalize(vPosition);
        vec3 normalizedHVector = normalize(normalizedIVector + normalizedOVector);
        float intensity = uLightPower / (pow(length(uLightPos - vPosition), 2.0) / 5.0 + 5.0);
        float dotProductNH = dot(normalizedNVector, normalizedHVector);

        // Get the fragColor based on the value of the dotProductNI
        if(dotProductNI > 0.0)
        {
            gl_FragColor = vec4(uDiffuseColor * (intensity * dotProductNI + uAmbient) + (intensity * pow(dotProductNH, uExponent)), 1.0);
        }
        else
        {
            gl_FragColor = vec4(uDiffuseColor * uAmbient, 1.0);
        }
    }
</script>

<!-- Fragment shader: microfacet model -->
<script id="shader-fs4" type="x-shader/x-fragment">
    precision mediump float;

    uniform vec3 uLightPos;             // Light position in camera space
    uniform float uLightPower;          // Light power
    uniform vec3 uDiffuseColor;         // Diffuse color
    uniform float uBeta;                // Roughness
    uniform float uIOR;                 // Index of refraction
    uniform float uAmbient;             // Ambient

    varying vec3 vPosition;             // Fragment position (camera space)
    varying vec3 vNormal;               // Fragment normal (camera space)

    void main(void) {
        // Calculate the dot product of NI 
        vec3 normalizedIVector = normalize(uLightPos - vPosition);
        vec3 normalizedNVector = normalize(vNormal);
        float dotProductNI = dot(normalizedNVector, normalizedIVector);

        gl_FragColor = vec4(uDiffuseColor * uAmbient, 1.0);

        if(dotProductNI > 0.0)
        {
            // Calculate some necessary unit vectors
            vec3 normalizedOVector = -normalize(vPosition);
            vec3 normalizedHVector = normalize(normalizedIVector + normalizedOVector);

            // Calculate some floats needed to compute fragColor
            float intensity = uLightPower / (pow(length(uLightPos - vPosition), 2.0) / 5.0 + 5.0);
            float dotProductNI = dot(normalizedNVector, normalizedIVector);
            float dotProductNO = dot(normalizedNVector, normalizedOVector);

            // Calculate the Fresnel Factor (F(ihat,hhat))
            float c = dot(normalizedIVector, normalizedHVector);
            float g = sqrt(pow(uIOR, 2.0) - 1.0 + pow(c, 2.0));
            float fresnelFactorMultiple1 = pow(g - c, 2.0) / (2.0 * pow(g + c, 2.0));
            float fresnelFactorMultiple2 = 1.0 + pow((c * (g + c) - 1.0) / (c * (g - c) + 1.0), 2.0);
            float fresnelFactor = fresnelFactorMultiple1 * fresnelFactorMultiple2;

            // Calculate the GGX normal distribution function (D(hhat))
            float dotProductNHSquared = pow(dot(normalizedNVector, normalizedHVector), 2.0);
            float betaSquared = pow(uBeta, 2.0);
            float denomMultiple = pow((betaSquared + (1.0/dotProductNHSquared - 1.0)), 2.0);
            const float PI = 3.14159265358979323846;
            float GGXNormDistrFunc = betaSquared / (PI * pow(dotProductNHSquared, 2.0) * denomMultiple);

            // Calculate the shadow-masking function (G(ihat, ohat, hhat))
            float G1IH = 2.0 / (1.0 + sqrt(1.0 + betaSquared * (1.0/pow(c, 2.0) - 1.0)));
            float dotProductOHSquared = pow(dot(normalizedOVector, normalizedHVector), 2.0);
            float G1OH = 2.0 / (1.0 + sqrt(1.0 + betaSquared * (1.0/dotProductOHSquared - 1.0)));
            float shadowMaskingFunc = G1IH * G1OH;

            // Get the fragColor based on the value of the dotProductNI
            float kdAddend = (fresnelFactor * GGXNormDistrFunc * shadowMaskingFunc) / (4.0 * dotProductNI * dotProductNO);
            gl_FragColor = gl_FragColor + vec4(intensity * dotProductNI * (uDiffuseColor + kdAddend), 1.0);
        }
    }
</script>


<!-- Fragment Shader: Cook-Torrance BRDF (Bidirectional Reflectance Distribution Function) -->
<script id="shader-cooktor" type="x-shader/x-fragment">
    precision mediump float;

    uniform vec3 uLightPos;             // Light position in camera space
    uniform float uLightPower;          // Light power
    uniform vec3 uDiffuseColor;         // Diffuse color
    uniform float uBeta;                // Roughness
    uniform float uIOR;                 // Index of refraction
    uniform float uAmbient;             // Ambient

    varying vec3 vPosition;             // Fragment position (camera space)
    varying vec3 vNormal;               // Fragment normal (camera space)

    void main(void) {
        // Calculate the dot product of NI 
        vec3 normalizedIVector = normalize(uLightPos - vPosition);
        vec3 normalizedNVector = normalize(vNormal);
        float dotProductNI = dot(normalizedNVector, normalizedIVector);

        // Get the constant for both cases
        gl_FragColor = vec4(uDiffuseColor * uAmbient, 1.0);

        if(dotProductNI > 0.0)
        {
            // Calculate some necessary unit vectors
            vec3 normalizedOVector = -normalize(vPosition);
            vec3 normalizedHVector = normalize(normalizedIVector + normalizedOVector);

            // Calculate some floats needed to compute fragColor
            float intensity = uLightPower / (pow(length(uLightPos - vPosition), 2.0) / 5.0 + 5.0);
            float dotProductNO = dot(normalizedNVector, normalizedOVector);

            // Calculate the Fresnel Term using Schlick's approximation
            float reflectanceNomIncidence = pow((uIOR - 1.0)/(uIOR + 1.0), 2.0);
            float dotProductOH = dot(normalizedOVector, normalizedHVector);
            float fresnelFactor = reflectanceNomIncidence + (1.0 - reflectanceNomIncidence) * pow((1.0 - dotProductOH), 5.0);

            // Calculate the Normal Distribution Function
            float betaSquared = pow(uBeta, 2.0);
            float dotProductHN = dot(normalizedHVector, normalizedNVector);
            float expo = 2.0/betaSquared - 2.0;
            const float PI = 3.14159265358979323846;
            float normDistri = pow(dotProductHN, expo) / (PI * betaSquared);

            // Calculate the Geometric Attenuation
            float firstProduct = (2.0 * dotProductHN * dotProductNO / dotProductOH);
            float secondProduct = (2.0 * dotProductHN * dotProductNI / dotProductOH);
            float geometricAttenuation = min(min(1.0, firstProduct), secondProduct);

            // Calculate the fraction in the parentheses
            float fracNum = fresnelFactor * normDistri * geometricAttenuation;
            float fracDenom = 4.0 * dotProductNI * dotProductNO;
            float fracPart = fracNum / fracDenom;
            
            // Calculate the fragment color
            gl_FragColor = gl_FragColor + vec4(intensity * dotProductNI * (uDiffuseColor + fracPart), 1.0);
        }
    }
</script>


<!-- Disney Principled Shader (PBR Shader)-->
<script id="shader-disney" type="x-shader/x-fragment">
    precision mediump float;

    uniform vec3 uLightPos;             // Light position in camera space
    uniform float uLightPower;          // Light power
    uniform vec3 uDiffuseColor;         // Diffuse color
    uniform float uBeta;                // Roughness
    uniform float uIOR;                 // Index of refraction
    uniform float uAmbient;             // Ambient
    uniform float uMetallic;            // Metallic constant
    uniform float uSheen;               // Sheen constant
    uniform float uSheenTint;           // Sheen Tint
    uniform float uClearCoat;           // Clearcoat constant
    uniform float uClearCoatRoughness;   // Roughness of the ClearCoat

    varying vec3 vPosition;             // Fragment position (camera space)
    varying vec3 vNormal;               // Fragment normal (camera space)


    void main(void) {
        // Calculate the dot product of NI 
        vec3 normalizedIVector = normalize(uLightPos - vPosition);
        vec3 normalizedNVector = normalize(vNormal);
        float dotProductNI = dot(normalizedNVector, normalizedIVector);

        if(dotProductNI > 0.0)
        {
            // Calculate some necessary unit vectors
            vec3 normalizedOVector = -normalize(vPosition);
            vec3 normalizedHVector = normalize(normalizedIVector + normalizedOVector);

            // Calculate some floats needed to compute fragColor
            float intensity = uLightPower / (pow(length(uLightPos - vPosition), 2.0) / 5.0 + 5.0);
            // float dotProductNI = dot(normalizedNVector, normalizedIVector);

            // Calculate the Fresnel Factor Term of o and h using Schlick's approximation; 
            // Needed for the diffuse and specular contribution
            float reflectanceNomIncidence = pow((uIOR - 1.0)/(uIOR + 1.0), 2.0);
            float dotProductOH = dot(normalizedOVector, normalizedHVector);
            float fresnelFactor = reflectanceNomIncidence + (1.0 - reflectanceNomIncidence) * pow((1.0 - dotProductOH), 5.0);

            // Calculate the diffuse contribution (L_diffuse)
            const float PI = 3.14159265358979323846;
            vec3 L_diffuse = (1.0 - uMetallic) * uDiffuseColor * (1.0 - fresnelFactor) * (1.0 / PI);

            // Calculate the specular contribution using the Cook-Torrance BRDF

            // Calculate the Normal Distribution Function
            float betaSquared = pow(uBeta, 2.0);
            float dotProductHN = dot(normalizedHVector, normalizedNVector);
            float expo = 2.0/betaSquared - 2.0;
            float normDistri = pow(dotProductHN, expo) / (PI * betaSquared);

            // Calculate the Geometric Attenuation
            float dotProductNO = dot(normalizedNVector, normalizedOVector);
            float firstProduct = (2.0 * dotProductHN * dotProductNO / dotProductOH);
            float secondProduct = (2.0 * dotProductHN * dotProductNI / dotProductOH);
            float geometricAttenuation = min(min(1.0, firstProduct), secondProduct);

            // Calculate the final specular contribution (L_specular)
            float fracNum = fresnelFactor * normDistri * geometricAttenuation;
            float fracDenom = 4.0 * dotProductNI * dotProductNO;
            float L_specular = uMetallic * fracNum / fracDenom;

            // Calculate the sheen contribution
            // float sheenColor = pow((1.0 - dotProductOH), 5.0) * (fresnelFactor * uSheen);
            vec3 sheenColor = uSheen * mix(vec3(1.0), uDiffuseColor, uSheenTint);

            // Calculate the contribution of the clearcoat constant using Microfacet Reflectance Model

            // Calculate the Normal Distribution Function for clear coat
            float dotProductNHSquared = pow(dot(normalizedNVector, normalizedHVector), 2.0);
            float clearCoatRoughnessSquared = pow(uClearCoatRoughness, 2.0);
            float Dmultiple = pow(dotProductNHSquared * (1.0 - clearCoatRoughnessSquared) + 1.0, 2.0);
            float clearCoatD = clearCoatRoughnessSquared / (PI * Dmultiple);

            // Calculate the Fresnel Factor for clear coat
            float reflectanceClearCoat = reflectanceNomIncidence * uClearCoat;
            float clearCoatF = reflectanceClearCoat + (1.0 - reflectanceClearCoat) * pow((1.0 - dotProductOH), 5.0);

            // Calculate the final clear coat contribution (L_clearCoat)
            float fracNumClearCoat = clearCoatD * clearCoatF * geometricAttenuation;
            float L_clearCoat = uClearCoat * fracNumClearCoat / fracDenom;

            // Get the final outgoing radiance (L_0) for the Disney Principled Shader
            vec3 L_0 = L_diffuse + L_specular + sheenColor + L_clearCoat;

            // Calculate the final fragment color
            gl_FragColor = vec4(intensity * dotProductNI * (uDiffuseColor + L_0), 1.0);
        }
        else
        {
            gl_FragColor = vec4(uDiffuseColor * uAmbient, 1.0);
        }
    }
</script>
</head>


<!-- HTML contents -->
<body>
    <h1>CS112 Final Project: Cook-Torrance BRDF and Disney Principled Shader</h1>
    <p>
        Student name: Nicholas Robinson<br>
        Student ID: 50305638
    </p>

    <div class="droplist">
        <span>
            Object:
            <select onchange="changeActiveMesh(this);">
              <option value="0" selected="selected">Utah teapot</option>
              <option value="1">Stanford bunny</option>
            </select>
        </span>
        <span style="margin-left:30px">
            Resolution:
            <select onchange="changeResolution(this);">
              <option value="0">640x360</option>
              <option value="1" selected="selected">800x450</option>
              <option value="2">960x540</option>
            </select>
        </span>

        <table>
            <tr>
                <td width="200px"><input type="checkbox" onchange="changeAnimatedState(this.checked);">Animated object</td>
                <td>
                    <input id="sliderBar" type="range" min="1" max="36" value="6" onchange="updateSlider(this.value);" disabled>
                    (<span id="sliderAmount">60</span>&deg;/s)
                </td>
            </tr>
        </table>
    </div>


    <div style="margin-top:10px">
        <canvas id="canvas0" style="border:none;" width="800" height="450"></canvas>
    </div>

    <div class="panel">
        <p>Light Source Parameters</p>
        <table>
            <tr>
                <td colspan="2"><input type="checkbox" onchange="changeShowLightState(this.checked);">Draw light source (as a point)</td>
            </tr>
            <tr>
                <td width="200px"><input type="checkbox" onchange="changeAnimatedLightState(this.checked);">Animated light</td>
                <td>
                    <input id="sliderBarLight" type="range" min="1" max="36" value="6" onchange="updateSliderLight(this.value);" disabled>
                    (<span id="sliderAmountLight">60</span>&deg;/s)
                </td>
            </tr>
            <tr>
                <td>Light power</td>
                <td>
                    <input id="sliderBar_LightPower" type="range" min="1" max="20" value="10" onchange="updateSlider_LightPower(this.value);">
                    (<span id="sliderAmount_LightPower">5</span>)
                </td>
            </tr>
        </table>
    </div>

    <div class="panel" style="margin-left:10px">
        <p>Shading Parameters (Common)</p>
        <table>
            <tr>
                <td width="200px">Shader</td>
                <td>
                    <select onchange="changeActiveShader(this);">
                        <option value="0" selected="selected">White</option>
                        <option value="1">Diffuse Reflectance Model</option>
                        <option value="2">Phong Reflectance Model</option>
                        <option value="3">Blinn-Phong Reflectance Model</option>
                        <option value="4">(Isotropic) Microfacet Reflectance Model</option>
                        <option value="5">Cook-Torrance BRDF (Bidirectional Reflectance Distribution Function)</option>
                        <option value="6">Disney Principled Shader (PBR Shader) </option>
                    </select>
                </td>
            </tr>
            <tr>
                <td>Diffuse Color</td>
                <td>
                    <input type="text" id="colorPicker"> (<span id="colorText">#1f75fe</span>)
                </td>
            </tr>
            <tr>
                <td>Specular Color</td>
                <td>
                    <!-- Extra credit -->
                    #ffffff
                </td>
            </tr>
            <tr>
                <td>Ambient</td>
                <td>
                    <input type="range" min="0" max="50" value="10" onchange="updateSlider_Ambient(this.value);">
                    (<span id="sliderAmount_Ambient">0.1</span>)
                </td>
            </tr>

            <tr class="phong-panel" style="display:none">
                <td colspan="2"><hr><p>Shading Parameters (Phong)</p></td>
            </tr>
            <tr class="phong-panel" style="display:none">
                <td>Exponent</td>
                <td>
                    <input type="range" min="1" max="40" value="10" onchange="updateSlider_PhongExp(this.value);">
                    (<span id="sliderAmount_PhongExp">50</span>)
                </td>
            </tr>

            <tr class="blinn-phong-panel" style="display:none">
                <td colspan="2"><hr><p>Shading Parameters (Blinn-Phong)</p></td>
            </tr>
            <tr class="blinn-phong-panel" style="display:none">
                <td>Exponent</td>
                <td>
                    <input type="range" min="1" max="40" value="10" onchange="updateSlider_BlinnPhongExp(this.value);">
                    (<span id="sliderAmount_BlinnPhongExp">50</span>)
                </td>
            </tr>

            <tr class="microfacet-panel" style="display:none">
                <td colspan="2"><hr><p>Shading Parameters (Microfacet)</p></td>
            </tr>
            <tr class="microfacet-panel" style="display:none">
                <td>Index of Refraction</td>
                <td>
                    <input id="sliderBar_MicrofacetIOR" type="range" min="20" max="100" value="50" onchange="updateSlider_MicrofacetIOR(this.value);">
                    (<span id="sliderAmount_MicrofacetIOR">5.0</span>)
                </td>
            </tr>
            <tr class="microfacet-panel" style="display:none">
                <td>Beta</td>
                <td>
                    <input id="sliderBar_MicrofacetBeta" type="range" min="5" max="50" value="20" onchange="updateSlider_MicrofacetBeta(this.value);">
                    (<span id="sliderAmount_MicrofacetBeta">0.2</span>)
                </td>
            </tr>

            <tr class="cook-torrance-panel" style="display:none">
                <td colspan="2"><hr><p>Shading Parameters (Cook-Torrance)</p></td>
            </tr>
            <tr class="cook-torrance-panel" style="display:none">
                <td>Index of Refraction</td>
                <td>
                    <input id="sliderBar_CookTorranceIOR" type="range" min="20" max="100" value="50" onchange="updateSlider_CookTorranceIOR(this.value);">
                    (<span id="sliderAmount_CookTorranceIOR">5.0</span>)
                </td>
            </tr>
            <tr class="cook-torrance-panel" style="display:none">
                <td>Beta</td>
                <td>
                    <input id="sliderBar_CookTorranceBeta" type="range" min="5" max="50" value="20" onchange="updateSlider_CookTorranceBeta(this.value);">
                    (<span id="sliderAmount_CookTorranceBeta">0.2</span>)
                </td>
            </tr>
            
            <tr class="disney-shader-panel" style="display:none">
                <td colspan="7"><hr><p>Shading Parameters (Disney Shader)</p></td>
            </tr>
            <tr class="disney-shader-panel" style="display:none">
                <td>Index of Refraction</td>
                <td>
                    <input id="sliderBar_DisneyShaderIOR" type="range" min="20" max="100" value="50" onchange="updateSlider_DisneyShaderIOR(this.value);">
                    (<span id="sliderAmount_DisneyShaderIOR">5.0</span>)
                </td>
            </tr>
            <tr class="disney-shader-panel" style="display:none">
                <td>Beta</td>
                <td>
                    <input id="sliderBar_DisneyShaderBeta" type="range" min="5" max="50" value="20" onchange="updateSlider_DisneyShaderBeta(this.value);">
                    (<span id="sliderAmount_DisneyShaderBeta">0.2</span>)
                </td>
            </tr>
            <tr class="disney-shader-panel" style="display:none">
                <td>Metallic</td>
                <td>
                    <input id="sliderBar_DisneyShaderMetallic" type="range" min="0" max="100" value="30" onchange="updateSlider_DisneyShaderMetallic(this.value);">
                    (<span id="sliderAmount_DisneyShaderMetallic">0.3</span>)
                </td>
            </tr>
            <tr class="disney-shader-panel" style="display:none">
                <td>Sheen</td>
                <td>
                    <input id="sliderBar_DisneyShaderSheen" type="range" min="0" max="100" value="30" onchange="updateSlider_DisneyShaderSheen(this.value);">
                    (<span id="sliderAmount_DisneyShaderSheen">0.3</span>)
                </td>
            </tr>
            <tr class="disney-shader-panel" style="display:none">
                <td>Sheen Tint</td>
                <td>
                    <input id="sliderBar_DisneyShaderSheenTint" type="range" min="0" max="100" value="30" onchange="updateSlider_DisneyShaderSheenTint(this.value);">
                    (<span id="sliderAmount_DisneyShaderSheenTint">0.3</span>)
                </td>
            </tr>
            <tr class="disney-shader-panel" style="display:none">
                <td>Clearcoat</td>
                <td>
                    <input id="sliderBar_DisneyShaderClearCoat" type="range" min="0" max="100" value="30" onchange="updateSlider_DisneyShaderClearCoat(this.value);">
                    (<span id="sliderAmount_DisneyShaderClearCoat">0.3</span>)
                </td>
            </tr>
            <tr class="disney-shader-panel" style="display:none">
                <td>Clearcoat Roughness</td>
                <td>
                    <input id="sliderBar_DisneyShaderClearCoatRoughness" type="range" min="0" max="100" value="30" onchange="updateSlider_DisneyShaderClearCoatRoughness(this.value);">
                    (<span id="sliderAmount_DisneyShaderClearCoatRoughness">0.3</span>)
                </td>
            </tr>
        </table>
    </div>
    <div style="clear:left"></div>
</body>

</html>
